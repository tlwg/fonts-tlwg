AC_INIT(thaifonts-scalable.spec.in)
AM_INIT_AUTOMAKE(thaifonts-scalable, 0.3.1)

dnl
dnl Check options
dnl
AC_ARG_ENABLE(ttf,
              [  --enable-ttf            enable TTF generation [[default=yes]]],
              , enable_ttf="yes")
AC_ARG_WITH(ttfdir,
  [  --with-ttfdir=DIR    font directory in DIR [/usr/share/fonts/th/TTF]],
  [ttfdir="$withval"], [ttfdir=/usr/share/fonts/th/TTF])
AC_MSG_CHECKING(--with-ttfdir)
AC_MSG_RESULT("$ttfdir")

AC_SUBST(ttfdir)

AC_ARG_ENABLE(type1,
              [  --enable-type1          enable Type1 generation [[default=no]]],
              , enable_type1="no")
AC_ARG_WITH(type1dir,
  [  --with-type1dir=DIR    font directory in DIR [/usr/share/fonts/th/Type1]],
  [type1dir="$withval"], [type1dir=/usr/share/fonts/th/Type1])
AC_MSG_CHECKING(--with-type1dir)
AC_MSG_RESULT("$type1dir")

AC_SUBST(type1dir)

AM_CONDITIONAL(INCLUDE_TTF,test $enable_ttf = "yes")
AM_CONDITIONAL(INCLUDE_TYPE1,test $enable_type1 = "yes")

dnl
dnl Check Programs
dnl
AC_PROG_INSTALL
AC_PROG_LN_S
AC_CHECK_PROGS(RMDIR,rmdir,no)

AC_CHECK_PROGS(PFAEDIT,pfaedit,no)

# AC_CHECK_PROGS(TTMKFDIR,ttmkfdir,no)
# if test "x$TTMKFDIR" = "xno" ; then
#   AC_MSG_ERROR([You need ttmkfdir (from freetype distribution)])
# fi

dnl prefer ttmkfdir for the time being
if test "x$enable_ttf" = "xyes"; then
  AC_CHECK_PROG(MKFONTSCALE,ttmkfdir,ttmkfdir)
  AC_CHECK_PROG(MKFONTSCALE,mkfontscale,mkfontscale)
  if test "x$MKFONTSCALE" = "x"; then
    AC_MSG_ERROR([You need mkfontscale or ttmkfdir])
  elif test "$MKFONTSCALE" = "ttmkfdir"; then
    if (ttmkfdir -h 2>&1 | grep -- --encoding >/dev/null); then
      # ttmkfdir 2
      MKFONTSCALE="ttmkfdir -e /usr/X11R6/lib/X11/fonts/encodings/encodings.dir -o fonts.scale"
    else
      # ttmkfdir 1
      MKFONTSCALE="ttmkfdir > fonts.scale"
    fi
  fi
  AC_MSG_RESULT([Will create fonts.scale using $MKFONTSCALE])
fi

if test "x$enable_type1" = "xyes"; then
  AC_CHECK_PROG(TYPE1INST,type1inst,type1inst)
  if test "x$TYPE1INST" = "x"; then
    AC_MSG_ERROR([You need type1inst])
  fi
fi
AC_CHECK_PROG(FCCACHE,fc-cache,[fc-cache -f || true])

AC_OUTPUT(
  Makefile
  nf/Makefile
  db/Makefile
  scripts/Makefile
  scripts/gen.mk
  thaifonts-scalable.spec
)


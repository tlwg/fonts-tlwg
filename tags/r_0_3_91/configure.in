AC_INIT(thaifonts-scalable.spec.in)
AM_INIT_AUTOMAKE(thaifonts-scalable, 0.3.91)

dnl
dnl Check options
dnl
AC_ARG_ENABLE(ttf,
              [  --enable-ttf            enable TTF generation [[default=yes]]],
              , enable_ttf="yes")
AC_ARG_WITH(ttfdir,
  [  --with-ttfdir=DIR    font directory in DIR [PREFIX/share/fonts/th/TTF]],
  [ttfdir="$withval"], [ttfdir=\$\{prefix\}/share/fonts/th/TTF])
AC_MSG_CHECKING(--with-ttfdir)
AC_MSG_RESULT("$ttfdir")

AC_SUBST(ttfdir)

AC_ARG_ENABLE(type1,
              [  --enable-type1          enable Type1 generation [[default=no]]],
              , enable_type1="no")
AC_ARG_WITH(type1dir,
  [  --with-type1dir=DIR    font directory in DIR [PREFIX/share/fonts/th/Type1]],
  [type1dir="$withval"], [type1dir=\$\{prefix\}/share/fonts/th/Type1])
AC_MSG_CHECKING(--with-type1dir)
AC_MSG_RESULT("$type1dir")

AC_SUBST(type1dir)

AM_CONDITIONAL(INCLUDE_TTF,test $enable_ttf = "yes")
AM_CONDITIONAL(INCLUDE_TYPE1,test $enable_type1 = "yes")

dnl
dnl Check Programs
dnl
AC_PROG_INSTALL
AC_PROG_LN_S
AC_CHECK_PROGS(RMDIR,rmdir,no)

AC_CHECK_PROGS(PFAEDIT,pfaedit,no)

if test "x$PFAEDIT" = "xno"; then
  AC_MSG_ERROR([You need pfaedit])
fi

# AC_CHECK_PROGS(TTMKFDIR,ttmkfdir,no)
# if test "x$TTMKFDIR" = "xno" ; then
#   AC_MSG_ERROR([You need ttmkfdir (from freetype distribution)])
# fi

dnl prefer mkfontscale over ttmkfdir
AC_CHECK_PROG(MKFONTSCALE,mkfontscale,[mkfontscale -e tis620-0 -e tis620-2])

if test "x$enable_ttf" = "xyes"; then
  if test "x$MKFONTSCALE" = "x"; then
    AC_CHECK_PROG(TTMKFDIR,ttmkfdir,ttmkfdir)
    if test "x$MKFONTSCALE" = "x"; then
      AC_MSG_ERROR([You need mkfontscale or ttmkfdir])
    else
      if (ttmkfdir -h 2>&1 | grep -- --encoding >/dev/null); then
        # ttmkfdir 2
        TTMKFDIR="ttmkfdir -e /usr/X11R6/lib/X11/fonts/encodings/encodings.dir -o fonts.scale"
      else
        # ttmkfdir 1
        TTMKFDIR="ttmkfdir > fonts.scale"
      fi
    fi
  else
    TTMKFDIR="$MKFONTSCALE"
  fi
  AC_MSG_RESULT([Will create fonts.scale for TTF using $TTMKFDIR])
fi

if test "x$enable_type1" = "xyes"; then
  if test "x$MKFONTSCALE" = "x"; then
    AC_CHECK_PROG(TYPE1INST,type1inst,type1inst)
    if test "x$TYPE1INST" = "x"; then
      AC_MSG_ERROR([You need mkfontscale or type1inst])
    fi
  else
    TYPE1INST="$MKFONTSCALE"
  fi
  AC_MSG_RESULT([Will create fonts.scale for Type1 using $TYPE1INST])
fi
AC_CHECK_PROG(FCCACHE,fc-cache,[fc-cache -f || true])

AC_OUTPUT(
  Makefile
  nf/Makefile
  db/Makefile
  scripts/Makefile
  scripts/gen.mk
  thaifonts-scalable.spec
)

